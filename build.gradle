plugins {
    id "application"
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    implementation 'org.json:json:20220924'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
}

application {
    mainClass = 'de.pdinklag.mcstats.cli.MinecraftStatsCLI'
}

task copyWww(type: Copy) {
    from('www') {
        include 'index.html'
        include 'style.min.css'
        include 'font/*.woff2'
        include 'img/**/*.gif'
        include 'img/**/*.png'
        include 'js/lib/**'
        include 'js/mcstats.min.js'
        include 'js/mcstats.min.js.map'
        include 'localization/*.json'
    }
    into 'build/www'
}

task jarCli (type: Jar) {
    archiveBaseName = "MinecraftStatsCLI"

    dependsOn compileJava
    dependsOn processResources
    manifest {
        attributes(
            'Main-Class': application.mainClass
        )
    }
    from sourceSets.main.runtimeClasspath.collect { it.getName().endsWith(".jar") ? zipTree(it) : it }
    exclude 'de/pdinklag/mcstats/bukkit'
    exclude '*.yml'
}

task jarPlugin (type: Jar) {
    archiveBaseName = "MinecraftStats"

    dependsOn compileJava
    dependsOn processResources
    dependsOn copyWww
    from('build') {
        include 'www/**/*'
    }
    from sourceSets.main.runtimeClasspath.collect { it.getName().endsWith(".jar") ? zipTree(it) : it }
    exclude 'de/pdinklag/mcstats/cli'
}

jar {
    actions = []
    dependsOn jarCli
    dependsOn jarPlugin
}
